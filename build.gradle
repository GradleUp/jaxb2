plugins {
  id "groovy"
  id "java-gradle-plugin"
  id "maven-publish"
  id "com.gradle.plugin-publish" version "0.10.1"
  id "com.cinnober.gradle.semver-git" version "2.4.0"
}

sourceSets {
  functionalTest {
    java {
      srcDir file('src/functionalTest/java')
    }
    resources {
      srcDir file('src/functionalTest/resources')
    }
    compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
    runtimeClasspath += output + compileClasspath
  }
}

gradlePlugin {
  plugins {
    jaxb2Plugin {
      id = 'com.github.gradlecommunity.jaxb2'
      implementationClass = "com.ewerk.gradle.plugins.Jaxb2Plugin"
      displayName = 'Gradle JAXB2 plugin'
      description = 'Plugin for generating Java sources from XML schemas files.'
    }
  }
  testSourceSets sourceSets.functionalTest
}

pluginBundle {
  website = 'https://github.com/gradle-community/jaxb2'
  vcsUrl = 'https://github.com/gradle-community/jaxb2'
  description = 'Plugin for generating Java sources from XML schemas files.'
  tags = ['jaxb2', 'jaxb']
}

group = "com.github.gradlecommunity.jaxb2"

sourceCompatibility=1.8
targetCompatibility=1.8

ext.hamcrestVersion=1.3
ext.junitVersion=4.12

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  implementation localGroovy()

  testImplementation "org.hamcrest:hamcrest-core:$hamcrestVersion"
  testImplementation "junit:junit:$junitVersion"
  testImplementation gradleTestKit()
}

test {
}

task functionalTest(type: Test) {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

//check.dependsOn functionalTest
